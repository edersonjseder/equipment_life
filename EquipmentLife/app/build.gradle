apply plugin: 'com.android.application'

android {
    signingConfigs {
        config_release {
            def propertiesFile = file('../local.properties')
            if (propertiesFile.exists()) {

                def localProps = new Properties()
                localProps.load(new FileInputStream(propertiesFile))

                def Properties keyProps = new Properties()
                assert localProps['keystore.props.file'];

                keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))

                storeFile = file(keyProps['STORE_FILE'])
                storePassword = keyProps['STORE_PASSWORD']
                keyAlias = keyProps['KEY_ALIAS']
                keyPassword = keyProps['KEY_PASSWORD']
            }
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.life.equipmentlife"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.config_release
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config_release
            applicationVariants.all { variant ->
                variant.outputs.all {
                    output ->
                        outputFileName = new File("Equipment_Life_Cam-" + variant.name + "-" + variant.versionName + ".apk");
                }

            }
        }
        debug {
            signingConfig signingConfigs.config_release
        }
    }
    flavorDimensions "version"
    productFlavors {
        free {
            dimension "version"
            applicationId "com.life.equipmentlife.free"
            signingConfig signingConfigs.config_release
        }
        paid {
            dimension "version"
            applicationId "com.life.equipmentlife.paid"
            signingConfig signingConfigs.config_release
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-joda:2.4.0') {
        //exclude module: ''
    }
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.android.support:design:$androidSupportVersion"
    implementation "com.android.support:palette-v7:$androidSupportVersion"
    implementation "com.android.support:recyclerview-v7:$androidSupportVersion"
    implementation "com.android.support:cardview-v7:$androidSupportVersion"

    // Used to map the Json with java objects
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.6.3'

    //3rd Libary - Inject Android object and get reference.
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'android.arch.persistence.room:runtime:1.0.0'
    annotationProcessor 'android.arch.persistence.room:compiler:1.0.0'
    implementation 'android.arch.lifecycle:extensions:1.1.0'
    annotationProcessor 'android.arch.lifecycle:compiler:1.1.0'
    implementation 'com.google.code.gson:gson:2.6.2'
    implementation 'de.hdodenhof:circleimageview:1.2.1'
    // circular imageview
    implementation 'com.mikhaellopez:circularimageview:3.2.0'

    //dexter permissions
    implementation "com.karumi:dexter:5.0.0"

    implementation 'com.github.yalantis:ucrop:2.2.2'

    // Library to make HTTP connections via Rest
    implementation 'com.squareup.okhttp3:okhttp:3.4.1'
    implementation 'com.squareup.retrofit2:converter-jackson:2.0.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.0.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    implementation 'com.google.code.gson:gson:2.6.2'

    // Firebase cloud storage
    implementation 'com.firebase:firebase-client-android:2.5.2'
    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.google.firebase:firebase-storage:16.0.1'
    implementation 'com.google.firebase:firebase-auth:16.0.1'
    implementation 'com.google.firebase:firebase-database:16.0.1'
    implementation 'com.google.android.gms:play-services-auth:15.0.1'
    implementation 'com.facebook.android:facebook-login:[5,6)'
    freeImplementation 'com.google.android.gms:play-services-ads:15.0.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

apply plugin: 'com.google.gms.google-services'
